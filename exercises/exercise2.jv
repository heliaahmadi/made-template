

valuetype StadtteilType oftype text {
    constraints: [StadtteilRegex];
}

constraint StadtteilRegex oftype RegexConstraint {
    regex: /^Furth-.*/;  
}

valuetype GeocoordinatesType oftype text {
    constraints: [GeocoordinatesRegex];
}

constraint GeocoordinatesRegex oftype RegexConstraint {
    regex: /\d{1,3}\.\d+,\s*\d{1,3}\.\d+/; 
}


pipeline TreesPipeline {

    block TreesExtractor oftype HttpExtractor { 
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv"; 
    }
    
    block TreesTextFileInterpreter oftype TextFileInterpreter {}
    
    block TreesCSVInterpreter oftype CSVInterpreter {
         delimiter: ";";
    }

    block DropColumn oftype ColumnDeleter {
        delete: [column E];
    }

    block TreesTableInterpreter oftype TableInterpreter {
        header: true; 
    
        columns: [
            "lfd_nr" oftype integer, 
            "stadtteil" oftype StadtteilType,
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "baumart_deutsch" oftype text,
            "id" oftype GeocoordinatesType,
            "baumfamilie" oftype text
        ];
    }
    
    block TreesDatabaseLoader oftype SQLiteLoader { 
        table: "Trees"; 
        file: "./trees.sqlite"; 
    }

    TreesExtractor
    -> TreesTextFileInterpreter
    -> TreesCSVInterpreter
    -> DropColumn
    -> TreesTableInterpreter
    -> TreesDatabaseLoader;
}
